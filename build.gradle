buildscript {
    ext {
        springBootVersion = '2.6.7'
        kotlinVersion = '1.6.21'
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id "org.sonarqube" version "3.3"
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.plugin.spring" version "${kotlinVersion}"
    id "org.jlleitschuh.gradle.ktlint" version "10.2.1"
}

apply plugin: 'org.springframework.boot'

group = 'com.tibiawiki'
version = '2.0.0'
description = 'TibiaWikiApi'
//sourceCompatibility = '16'

repositories {
    mavenLocal()
    mavenCentral()

//    maven {
//        url = 'https://maven.pkg.github.com/benjaminkomen/jwiki'
//        credentials {
//            username = 'benjaminkomen'
//            password = System.getenv("GITHUB_TOKEN")
//        }
//    }
}

ext {
    slf4jVersion = '1.7.36'
    junitVersion = '5.8.2'
    lombokVersion = '1.18.24'
    jacksonVersion = '2.13.2'
    jacocoVersion = "0.8.7"
    mockitoVersion = "4.5.1"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonVersion}"
    implementation "org.springframework.boot:spring-boot-starter:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
//    implementation "benjaminkomen:jwiki:2.2.0"
    implementation "io.github.fastily:jwiki:1.10.0"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "net.logstash.logback:logstash-logback-encoder:7.1.1"
    implementation "ch.qos.logback:logback-classic:1.2.11"
    implementation "org.springframework.cloud:spring-cloud-gcp-starter-logging:1.2.8.RELEASE"
    implementation "org.codehaus.janino:janino:3.1.8"
    implementation "org.json:json:20220320"
    implementation "com.google.guava:guava:31.1-jre"
    implementation "io.vavr:vavr:0.10.4"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-inline:${mockitoVersion}"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}") {
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    implementation "io.swagger.core.v3:swagger-annotations:2.2.0"
    implementation "org.springdoc:springdoc-openapi-ui:1.6.8"
}

//java {
//    sourceCompatibility = JavaVersion.VERSION_16
//    targetCompatibility = JavaVersion.VERSION_16
//}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "TibiaWikiApi"
        property "sonar.projectKey", "com.tibiawiki:TibiaWikiApi"
        property "sonar.organization", "benjaminkomen-github"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "src/main"
        property "sonar.tests", "src/test"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "./build/reports/jacocoTestReport.xml"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed", "standard_error"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com/tibiawiki/TibiaWikiApiApplication'
    }
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestImplementation.extendsFrom testImplementation
}

springBoot {
    buildInfo()
}

idea {
    module {
        testSourceDirs += sourceSets.integrationTest.java.srcDirs
        testResourceDirs += sourceSets.integrationTest.resources.srcDirs
//        scopes.TEST.plus += [configurations.integrationTestCompile]
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    sourceCompatibility = 17
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

check.dependsOn integrationTest

jacoco {
    toolVersion = "${jacocoVersion}"
}

jacocoTestReport {
    dependsOn check

    reports {
        xml.enabled = true
        xml.destination = file("${buildDir}/reports/jacocoTestReport.xml")
    }
}
