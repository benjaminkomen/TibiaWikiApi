buildscript {
    ext {
        springBootVersion = '2.4.1'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id "org.sonarqube" version "3.0"
}

apply plugin: 'org.springframework.boot'

group = 'com.tibiawiki'
version = '1.10.0'
description = 'TibiaWikiApi'
sourceCompatibility = '15'
targetCompatibility = '15'

repositories {
    mavenLocal()
    mavenCentral()

//    maven {
//        url = 'https://maven.pkg.github.com/benjaminkomen/jwiki'
//        credentials {
//            username = 'benjaminkomen'
//            password = System.getenv("GITHUB_TOKEN")
//        }
//    }
}

ext {
    slf4jVersion = '1.7.30'
    junitVersion = '5.7.0'
    lombokVersion = '1.18.16'
    jacocoVersion = "0.8.6"
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
//    implementation "benjaminkomen:jwiki:2.2.0"
    implementation "org.fastily:jwiki:1.8.0"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    implementation "org.json:json:20201115"
    implementation "com.google.guava:guava:30.1-jre"
    implementation "io.vavr:vavr:0.10.3"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.mockito:mockito-core:3.6.28"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}") {
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    implementation "io.swagger.core.v3:swagger-annotations:2.1.6"
    implementation "org.springdoc:springdoc-openapi-ui:1.5.2"
}

configurations {
    all*.exclude module : 'spring-boot-starter-logging'
    all*.exclude module : 'logback-classic'
}


sonarqube {
    properties {
        property "sonar.projectName", "TibiaWikiApi"
        property "sonar.projectKey", "com.tibiawiki:TibiaWikiApi"
        property "sonar.organization", "benjaminkomen-github"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "src/main/java"
        property "sonar.tests", "src/test/java"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "./build/reports/jacocoTestReport.xml"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed", "standard_error"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com/tibiawiki/TibiaWikiApiApplication'
    }
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output + configurations.testCompile
        runtimeClasspath += output + compileClasspath + configurations.testRuntime
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestImplementation.extendsFrom testImplementation
}

springBoot {
    buildInfo()
}

idea {
    module {
        testSourceDirs += sourceSets.integrationTest.java.srcDirs
        testResourceDirs += sourceSets.integrationTest.resources.srcDirs
        scopes.TEST.plus += [configurations.integrationTestCompile]
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

check.dependsOn integrationTest

jacoco {
    toolVersion = "${jacocoVersion}"
}

jacocoTestReport {
    dependsOn check

    reports {
        xml.enabled = true
        xml.destination = file("${buildDir}/reports/jacocoTestReport.xml")
    }
}
